+++
title = "RAG Simples com Clojure e Ollama"
description = "Um prot√≥tipo funcional do zero"
date = 2024-01-23T19:00:00-00:00
tags = ["RAG", "LLM", "AI", "Langchain"]
draft = false
weight = 1
author = "Vitor Lobo Ramos"
+++

# Sum√°rio

- **[O que √© RAG e por que precisamos dele?](#o-que-√©-rag-e-por-que-precisamos-dele)**
    - [Por que isso √© um problema?](#por-que-isso-√©-um-problema)
    - [E √© a√≠ que entra o RAG!](#e-√©-a√≠-que-entra-o-rag)
    - **[Construindo uma aplica√ß√£o RAG simples](#construindo-uma-aplica√ß√£o-rag-simples)**
        - [Preparando o ambiente](#preparando-o-ambiente)
        - [Estrutura do projeto](#estrutura-do-projeto)
        - [Usando Ollama para LLMs locais sem API keys](#usando-ollama-para-llms-locais-sem-api-keys)
        - [Como usar?](#como-usar)
- **[Considera√ß√µes importantes](#considera√ß√µes-importantes)**
- **[Pr√≥ximos passos](#pr√≥ximos-passos)**
- **[Langchain4j para simplificar a cria√ß√£o de RAG](#langchain4j-para-simplificar-a-cria√ß√£o-de-rag)**


Ol√°, pessoal! üëã 

Neste artigo, vamos explorar como construir uma aplica√ß√£o [RAG (Retrieval-Augmented Generation)](https://pt.wikipedia.org/wiki/Gera√ß√£o_aumentada_por_recupera√ß√£o) completa do zero usando [Clojure](https://clojure.org/). Vamos mergulhar em uma implementa√ß√£o pr√°tica que combina processamento de texto, busca sem√¢ntica e gera√ß√£o de respostas com LLMs locais. Se voc√™ est√° interessado em melhorar a precis√£o e relev√¢ncia das respostas dos seus modelos de linguagem com informa√ß√µes atualizadas, este guia √© para voc√™!

## O que √© RAG e por que precisamos dele? 

Os Modelos de Linguagem de Grande Escala (LLMs), como o GPT, ChatGPT e outros, revolucionaram a forma como interagimos com a intelig√™ncia artificial. Eles s√£o capazes de gerar textos coerentes, responder perguntas complexas e at√© mesmo criar conte√∫do criativo. No entanto, esses modelos possuem uma limita√ß√£o fundamental: seu conhecimento √© "congelado" no tempo.

Quando um LLM √© treinado, ele absorve informa√ß√µes dispon√≠veis at√© um determinado momento - sua data de corte de treinamento. Ap√≥s esse ponto, o modelo n√£o tem acesso a nenhuma informa√ß√£o nova. √â como se ele tivesse uma foto do mundo quando foi treinado, e n√£o conseguisse ver nada que aconteceu depois disso. Por exemplo, se um LLM foi treinado em 2022, ele n√£o vai saber nada sobre:

- Eventos que aconteceram em 2023
- Novas tecnologias que surgiram
- Aquela s√©rie nova que todo mundo t√° assistindo
- O √∫ltimo filme que ganhou o Oscar


### Por que isso √© um problema? 

Ao desenvolver aplica√ß√µes inteligentes, como assistentes financeiros que precisam de cota√ß√µes de a√ß√µes em tempo real, chatbots de suporte que devem conhecer os produtos mais recentes da empresa ou sistemas de recomenda√ß√£o que se baseiam nas √∫ltimas tend√™ncias, nos deparamos com uma limita√ß√£o crucial dos Modelos de Linguagem de Grande Escala (LLMs) tradicionais: seu conhecimento est√°tico. 

O problema fundamental reside no fato de que esses modelos, por mais sofisticados que sejam, possuem uma base de conhecimento "congelada" no momento de seu treinamento. Eles carecem de acesso inerente a informa√ß√µes atualizadas, o que restringe drasticamente sua aplicabilidade em cen√°rios que exigem dados em tempo real ou conhecimento sobre eventos recentes.

> Confiar exclusivamente em um LLM "puro" nesses contextos resultar√° em respostas desatualizadas, potencialmente imprecisas e, consequentemente, em uma experi√™ncia do usu√°rio comprometida. A efic√°cia da aplica√ß√£o √© diretamente afetada. 

√â nesse contexto que a t√©cnica de Recupera√ß√£o de Gera√ß√£o Aumentada (RAG - Retrieval-Augmented Generation) se torna n√£o apenas vantajosa, mas essencial. RAG permite superar a limita√ß√£o temporal inerente aos LLMs, garantindo que as aplica√ß√µes inteligentes entreguem informa√ß√µes relevantes e atualizadas.

### E √© a√≠ que entra o RAG!

Imagine um LLM como um erudito com vasto conhecimento enciclop√©dico, mas que viveu isolado em uma biblioteca por d√©cadas. Ele possui um conhecimento profundo de muitos assuntos, mas est√° desatualizado sobre os eventos recentes e desenvolvimentos em diversas √°reas. O RAG, nesse contexto, seria como fornecer a esse erudito um par de √≥culos de √∫ltima gera√ß√£o que n√£o apenas corrigem sua vis√£o, mas tamb√©m o conectam a um fluxo constante de informa√ß√µes atualizadas. 

Ele agora pode ler jornais, artigos cient√≠ficos recentes, consultar bases de dados em tempo real e, assim, responder a perguntas com uma precis√£o e relev√¢ncia muito maiores.

**Os Tr√™s Pilares do RAG**:

1. **Conex√£o com uma base de dados atual:** Em vez de depender apenas do conhecimento est√°tico adquirido durante seu treinamento (que pode se tornar obsoleto rapidamente), o LLM ganha acesso a uma fonte de informa√ß√µes din√¢mica e constantemente atualizada. Isso pode ser uma base de dados de not√≠cias, um reposit√≥rio de documentos corporativos, uma cole√ß√£o de artigos cient√≠ficos, ou qualquer outra fonte relevante para a tarefa em quest√£o.

2. **Pesquisa em tempo real:** O LLM n√£o est√° mais limitado a "lembrar" de informa√ß√µes. Ele adquire a capacidade de "procurar" ativamente por dados relevantes para responder a uma pergunta ou gerar um texto. Isso √© semelhante a como n√≥s, humanos, usamos mecanismos de busca para encontrar informa√ß√µes que n√£o temos memorizadas. O LLM, equipado com RAG, pode formular consultas, analisar os resultados e selecionar as informa√ß√µes mais pertinentes.

3. **Combina√ß√£o de conhecimento base com dados novos:** Este √© o ponto crucial que diferencia o RAG de uma simples busca em uma base de dados. O LLM n√£o apenas recupera informa√ß√µes, mas tamb√©m as integra ao seu conhecimento pr√©-existente. Ele usa sua capacidade de racioc√≠nio e compreens√£o para contextualizar os novos dados, identificar contradi√ß√µes, e formular respostas coerentes e informadas. O erudito agora n√£o apenas l√™ as not√≠cias, mas as interpreta √† luz de seu vasto conhecimento.

Segundo um [whitepaper recente dos pesquisadores do Google](https://arxiv.org/abs/2309.01066), existem v√°rias t√©cnicas para turbinar o desempenho dos LLMs, e o RAG √© uma das mais promissoras. Isso ocorre porque o RAG aborda algumas das limita√ß√µes fundamentais desses modelos:

* **Redu√ß√£o de "alucina√ß√µes":** LLMs, sem acesso a informa√ß√µes externas, podem "inventar" respostas (as chamadas "alucina√ß√µes") quando confrontados com perguntas sobre t√≥picos que est√£o fora de seu conhecimento base. O RAG, ao fornecer dados factuais, diminui drasticamente a probabilidade de alucina√ß√µes.
* **Respostas mais atualizadas:** O conhecimento de um LLM treinado em um determinado momento fica congelado no tempo. O RAG garante que as respostas reflitam as informa√ß√µes mais recentes dispon√≠veis.
* **Maior transpar√™ncia e explicabilidade:** Ao citar as fontes de informa√ß√£o utilizadas, o RAG torna as respostas dos LLMs mais transparentes e verific√°veis. Isso aumenta a confian√ßa no modelo, pois podemos entender de onde vieram as informa√ß√µes.
* **Melhora no desempenho em tarefas especificas:** O RAG aumenta a capacidade do LLM de lidar com tarefas que requerem acesso e entendimento de bases de dados ou informa√ß√µes especificas, como responder perguntas sobre documentos ou dados de uma empresa.

> O RAG representa um avan√ßo significativo na evolu√ß√£o dos LLMs, permitindo que eles se tornem ferramentas mais confi√°veis, precisas e √∫teis para uma ampla gama de aplica√ß√µes. Ele transforma o LLM de um "sabe-tudo" desatualizado em um pesquisador √°gil e bem-informado, capaz de combinar conhecimento profundo com informa√ß√µes atualizadas em tempo real.

O RAG tamb√©m √© uma maneira de voc√™ nichar seu LLM em uma √°rea espec√≠fica, seja ela um assunto, uma empresa, uma linguagem, uma tecnologia, etc. O RAG √© uma t√©cnica que combina a capacidade de gera√ß√£o de texto dos LLMs com um sistema de recupera√ß√£o de informa√ß√µes. Em vez de depender apenas do conhecimento interno do modelo, o RAG busca informa√ß√µes relevantes em uma base de dados externa antes de gerar uma resposta. A imagem abaixo mostra o fluxo de um sistema RAG:

```mermaid
graph LR
    A[Documentos] --> B[Processamento de Documentos]
    B --> C[Armazenamento de Vetores]
    D[Consulta do Usu√°rio] --> E[Processamento da Consulta]
    E --> F[Recupera√ß√£o de Documentos Relevantes]
    C --> F
    F --> G[Contexto Aumentado]
    G --> H[LLM]
    H --> I[Resposta Final]
    
    style A fill:#f9d5e5,stroke:#333
    style B fill:#eeeeee,stroke:#333
    style C fill:#d3f8e2,stroke:#333
    style D fill:#f9d5e5,stroke:#333
    style E fill:#eeeeee,stroke:#333
    style F fill:#e3e2f9,stroke:#333
    style G fill:#d3f8e2,stroke:#333
    style H fill:#f9e2ae,stroke:#333
    style I fill:#c5e0f9,stroke:#333

```

O diagrama acima ilustra o fluxo de um sistema RAG, desde a ingest√£o de documentos at√© a gera√ß√£o da resposta final. Primeiro, os documentos s√£o processados e armazenados como vetores. Quando um usu√°rio faz uma consulta, o sistema processa essa pergunta, recupera os documentos mais relevantes do armazenamento vetorial e cria um contexto aumentado que √© enviado ao LLM.

---

### Construindo uma aplica√ß√£o RAG simples

Vamos construir uma aplica√ß√£o RAG simples usando Clojure! Nosso objetivo √© criar um assistente inteligente que possa responder perguntas sobre documenta√ß√£o t√©cnica de projetos open source. Vou te mostrar como fazer isso passo a passo.

#### Preparando o ambiente

Pre-requisitos:
- [Clojure](https://clojure.org/guides/getting_started): Linguagem de programa√ß√£o funcional que vamos usar para construir a aplica√ß√£o
- [Leiningen](https://leiningen.org/): Ferramenta de build para Clojure
- [Ollama](https://ollama.com/): Modelo de linguagem local

Primeiro, vamos criar um novo projeto Clojure usando Leiningen:

```bash
lein new app docai
cd docai
```

Agora, vamos adicionar as depend√™ncias necess√°rias no arquivo `project.clj`:

```clojure
(defproject docai "0.1.0-SNAPSHOT"
  :description "Um assistente RAG para consulta de documenta√ß√£o t√©cnica"
  :url "http://example.com/FIXME"
  :license {:name "EPL-2.0 OR GPL-2.0-or-later WITH Classpath-exception-2.0"
            :url "https://www.eclipse.org/legal/epl-2.0/"}
  :dependencies [[org.clojure/clojure "1.11.1"]
                 [markdown-to-hiccup "0.6.2"]    ; Para processar Markdown
                 [hickory "0.7.1"]              ; Para processar HTML
                 [org.clojure/data.json "2.4.0"]  ; Para JSON
                 [http-kit "2.6.0"]             ; Para requisi√ß√µes HTTP
                 [org.clojure/tools.logging "1.2.4"]  ; Para logging
                 [org.clojure/tools.namespace "1.4.4"]  ; Para reloading
                 [org.clojure/core.async "1.6.681"]  ; Para opera√ß√µes ass√≠ncronas
                 [org.clojure/core.memoize "1.0.257"]  ; Para cache
                 [org.clojure/core.cache "1.0.225"]]  ; Para cache
  :main ^:skip-aot docai.core
  :target-path "target/%s"
  :profiles {:uberjar {:aot :all
                       :jvm-opts ["-Dclojure.compiler.direct-linking=true"]}})
```

> Curiosidade: Porque Clojure? Por ser uma linguagem funcional, facilita a implementa√ß√£o de pipelines de processamento de dados.

#### Estrutura do projeto

Nossa aplica√ß√£o ter√° tr√™s componentes principais:
1. **Processamento de documenta√ß√£o (Markdown/HTML)**
   - Extra√ß√£o de texto
   - Pr√©-processamento de texto
2. **Sistema de embeddings**
   - Cria√ß√£o de embeddings para o texto usando [TF-IDF](https://pt.wikipedia.org/wiki/TF-IDF)
   - Busca por similaridade sem√¢ntica
3. **Interface com o LLM**
   - Gera√ß√£o de resposta usando o LLM

> **Observa√ß√£o:** Embora o RAG moderno utilize embeddings densos gerados por modelos de linguagem para capturar a sem√¢ntica de forma mais rica, neste artigo, usaremos uma implementa√ß√£o simplificada de [TF-IDF (Term Frequency-Inverse Document Frequency)](https://pt.wikipedia.org/wiki/TF-IDF). 

O TF-IDF √© uma t√©cnica que nos permite representar documentos como vetores, calculando a import√¢ncia de cada palavra com base em sua frequ√™ncia no documento e em todo o corpus. Isso nos permite realizar uma busca por similaridade, sem depender de APIs externas para a gera√ß√£o de embeddings. √â importante ressaltar que essa √© uma abordagem did√°tica e simplificada. Para um sistema RAG de produ√ß√£o, o ideal seria utilizar embeddings gerados pelo pr√≥prio modelo de linguagem (ou um modelo compat√≠vel), em conjunto com um banco de dados vetorial. 

No entanto, para a parte de gera√ß√£o de respostas, continuaremos usando o [Ollama](https://ollama.com/) com o modelo [deepseek-r1](https://ollama.com/models/deepseek-r1). Vamos criar os namespaces necess√°rios come√ßando pelo m√≥dulo de processamento de documentos:

```clojure
;; src/docai/document.clj
(ns docai.document
  (:require [markdown-to-hiccup.core :as md]
            [hickory.core :as html]
            [clojure.string :as str]))

(defn is-string? [x]
  (instance? String x))

(defn extract-text-from-markdown [content]
  (try
    (let [hiccup-result (md/md->hiccup content)
          text-nodes (filter is-string? (flatten hiccup-result))]
      text-nodes)
    (catch Exception e
      (println "Erro ao processar Markdown:" (.getMessage e))
      [content])))

(defn extract-text-from-html [content]
  (try
    (let [dom (html/parse content)
          hiccup-result (html/as-hiccup dom)
          text-nodes (filter is-string? (flatten hiccup-result))]
      text-nodes)
    (catch Exception e
      (println "Erro ao processar HTML:" (.getMessage e))
      [content])))

(defn extract-text
  "Extrai texto de documenta√ß√£o (Markdown ou HTML)"
  [doc-path]
  (println "Extraindo texto de:" doc-path)
  (let [content (slurp doc-path)
        _ (println "Tamanho do conte√∫do:" (count content) "caracteres")
        _ (println "Amostra do conte√∫do:" (subs content 0 (min 100 (count content))))
        text (if (.endsWith doc-path ".md")
               (extract-text-from-markdown content)
               (extract-text-from-html content))
        _ (println "Quantidade de n√≥s de texto extra√≠dos:" (count text))
        chunks (partition-all 512 text)]  ; 512 tokens por chunk
    (println "Quantidade de chunks gerados:" (count chunks))
    chunks))

(defn preprocess-chunks
  "Limpa e prepara os chunks de texto"
  [chunks]
  (let [processed (map #(-> %
                            (str/join " ")
                            (str/replace #"\s+" " ")
                            (str/trim))
                       chunks)]
    (println "Primeiro chunk processado:" (first processed))
    processed))
```

Em seguida, vamos implementar o m√≥dulo de embeddings que vai permitir procurar informa√ß√µes semanticamente relevantes. Aqui estamos usando uma implementa√ß√£o pr√≥pria de TF-IDF, que √© uma t√©cnica eficiente para representar documentos em vetores, sem depender de APIs externas:

```clojure
;; src/docai/embedding.clj
(ns docai.embedding
  (:require [clojure.string :as str]
            [clojure.core.memoize :as memo]))

;; Implementa√ß√£o de embeddings usando TF-IDF simples
;; N√£o depende de modelos externos, ao contr√°rio do Ollama que usa o deepseek-r1 para o LLM

(defn tokenize
  "Divide o texto em tokens"
  [text]
  (if (string? text)
    (-> text
        str/lower-case
        (str/split #"\s+")
        (->> (filter #(> (count %) 2))))
    []))

(defn term-freq
  "Calcula a frequ√™ncia dos termos"
  [tokens]
  (frequencies tokens))

(defn string-doc? [x]
  (instance? String x))

(defn doc-freq
  "Calcula a frequ√™ncia dos documentos"
  [docs]
  (let [string-docs (filter string-doc? docs)  ; Use our own predicate function
        _ (println (str "Processando " (count string-docs) " documentos v√°lidos de " (count docs) " total"))
        doc-tokens (map tokenize string-docs)  
        all-tokens (distinct (flatten doc-tokens))
        doc-count (count string-docs)]
    (if (zero? doc-count)
      {}
      (zipmap all-tokens
              (map #(count (filter (fn [tokens] (some #{%} tokens)) doc-tokens))
                   all-tokens)))))

(defn tf-idf
  "Calcula TF-IDF para um documento"
  [doc doc-freq]
  (if (empty? doc-freq)
    {}
    (let [tokens (tokenize doc)
          tf (term-freq tokens)
          n-docs (count (keys doc-freq))]
      (zipmap (keys tf)
              (map #(* (get tf %) (Math/log (/ n-docs (get doc-freq % 1))))
                   (keys tf))))))

(defn vectorize
  "Converte um documento em um vetor TF-IDF"
  [doc doc-freq]
  (let [tf-idf-scores (tf-idf doc doc-freq)]
    (if (empty? doc-freq)
      []
      (map #(get tf-idf-scores % 0.0)
           (keys doc-freq)))))

(defn create-embeddings
  "Gera embeddings para uma lista de textos usando TF-IDF"
  [texts]
  (try
    (let [doc-freq (doc-freq texts)]
      (map #(vectorize % doc-freq) texts))
    (catch Exception e
      (println "Erro ao criar embeddings: " (.getMessage e))
      (vec (repeat (count texts) [])))))

(defn cosine-similarity
  "Calcula a similaridade do cosseno entre dois vetores"
  [v1 v2]
  (if (or (empty? v1) (empty? v2))
    0.0
    (let [dot-product (reduce + (map * v1 v2))
          norm1 (Math/sqrt (reduce + (map #(* % %) v1)))
          norm2 (Math/sqrt (reduce + (map #(* % %) v2)))]
      (if (or (zero? norm1) (zero? norm2))
        0.0
        (/ dot-product (* norm1 norm2))))))

(defn similarity-search
  "Encontra os N chunks mais similares"
  [query-embedding doc-embeddings n]
  (if (or (empty? query-embedding) (empty? doc-embeddings))
    (take (min n (count doc-embeddings)) (range))
    (let [scores (map #(cosine-similarity query-embedding %) doc-embeddings)]
      (->> (map vector scores (range))
           (sort-by first >)
           (take n)
           (map second)))))
```

O c√≥digo acima implementa uma t√©cnica chamada [TF-IDF (Term Frequency-Inverse Document Frequency)](https://pt.wikipedia.org/wiki/TF-IDF) para criar representa√ß√µes vetoriais dos textos. Esta abordagem permite calcular a similaridade entre documentos sem depender de modelos externos. No entanto, para a parte de gera√ß√£o, ainda usamos o Ollama com o modelo deepseek-r1.

#### Usando Ollama para LLMs locais sem API keys

Uma vantagem importante dessa abordagem √© que vamos utilizar o [Ollama](https://ollama.com/) para executar nossos modelos localmente. O Ollama √© uma ferramenta incr√≠vel que permite rodar LLMs diretamente na sua m√°quina, sem depender de servi√ßos em nuvem ou API keys. Vamos implementar a interface com o Ollama:

```clojure
;; src/docai/llm.clj
(ns docai.llm
  (:require [clojure.data.json :as json]
            [org.httpkit.client :as http]))

(def ollama-url "http://localhost:11434/api/generate")
(def model-name "deepseek-r1") ; Modelo DeepSeek para melhor qualidade

(defn call-ollama-api
  "Chama a API do Ollama para gerar uma resposta"
  [prompt]
  (let [request-body {:model model-name
                      :prompt prompt
                      :stream false}
        options {:headers {"Content-Type" "application/json"}
                 :body (json/write-str request-body)}
        response @(http/post ollama-url options)]
    (if (= (:status response) 200)
      (-> response
          :body
          (json/read-str :key-fn keyword)
          :response)
      (str "Erro ao chamar a API do Ollama: " (:status response) " - " (:body response)))))

;; Fun√ß√µes de utilidade para uso futuro:
;;
;; extract-code-blocks: Extrai blocos de c√≥digo do texto usando regex
;; exemplo de uso:
;;   (extract-code-blocks "```clojure\n(+ 1 2)\n```") => ["(+ 1 2)"]
;;
;; extract-summary: Cria um resumo de texto com tamanho m√°ximo especificado
;; exemplo de uso:
;;   (extract-summary "# T√≠tulo\nConte√∫do longo..." 50) => "Conte√∫do longo..."

(defn format-prompt
  "Formata o prompt para o LLM"
  [context query]
  (str "Voc√™ √© um assistente especializado em documenta√ß√£o t√©cnica. Com base no seguinte contexto da documenta√ß√£o:\n\n"
       context
       "\n\nPergunta: " query
       "\n\nForne√ßa uma resposta t√©cnica precisa e, se poss√≠vel, inclua exemplos de c√≥digo. "
       "Se a documenta√ß√£o n√£o contiver informa√ß√µes relevantes para a pergunta, "
       "indique isso claramente e forne√ßa uma resposta geral com base em seu conhecimento."))

(defn generate-response
  "Gera resposta usando o LLM com base no contexto"
  [query context]
  (try
    (let [prompt (format-prompt context query)]
      (println "DEBUG - Enviando prompt para o Ollama usando o modelo" model-name)
      (call-ollama-api prompt))
    (catch Exception e
      (str "Erro ao gerar resposta: " (.getMessage e) 
           "\n\nPor favor, verifique se o Ollama est√° em execu√ß√£o no endere√ßo " 
           ollama-url 
           "\n\nVoc√™ pode iniciar o Ollama com o comando: ollama serve"))))
```

Note que o m√≥dulo `llm.clj` inclui fun√ß√µes utilit√°rias adicionais como `extract_code_blocks` para extrair blocos de c√≥digo das respostas e `extract_summary` para criar resumos do texto, al√©m de melhor tratamento de erros na comunica√ß√£o com o Ollama. Por fim, vamos criar o m√≥dulo principal que integra todos os componentes:

```clojure
;; src/docai/core.clj
(ns docai.core
  (:require [docai.document :as doc]
            [docai.embedding :as emb]
            [docai.llm :as llm]
            [clojure.java.io :as io]
            [clojure.string :as str])
  (:gen-class))

(def docs-path "resources/docs")

(defn load-documentation
  "Carrega todos os arquivos de documenta√ß√£o do diret√≥rio"
  []
  (->> (file-seq (io/file docs-path))
       (filter #(.isFile %))
       (map #(.getPath %))))

(defn setup-knowledge-base
  "Configura a base de conhecimento inicial"
  []
  (let [doc-files (load-documentation)
        _ (when (empty? doc-files)
            (println "Aviso: Nenhum arquivo de documenta√ß√£o encontrado em resources/docs/"))
        _ (doseq [file doc-files]
            (println "Arquivo encontrado:" file))
        all-chunks (mapcat doc/extract-text doc-files)
        processed-chunks (doc/preprocess-chunks all-chunks)
        _ (println (str "Processando " (count processed-chunks) " chunks de texto..."))
        _ (when (< (count processed-chunks) 5)
            (println "DEBUG - Primeiros chunks:")
            (doseq [chunk (take 5 processed-chunks)]
              (println (str "Chunk: '" (subs chunk 0 (min 50 (count chunk))) "...'"))))
        embeddings (emb/create-embeddings processed-chunks)]
    {:chunks processed-chunks
     :embeddings embeddings
     :original-files doc-files}))

(defn get-file-content
  "L√™ o conte√∫do completo de um arquivo"
  [file-path]
  (try
    (slurp file-path)
    (catch Exception _
      (println "Erro ao ler arquivo:" file-path)
      "")))

(defn query-rag
  "Processa uma query usando o pipeline RAG"
  [knowledge-base query]
  (println "DEBUG - Processando query:" query)
  (if (and (seq (:chunks knowledge-base)) 
           (seq (:embeddings knowledge-base)))
    (let [query-emb (first (emb/create-embeddings [query]))
          similar-idxs (emb/similarity-search query-emb 
                                            (:embeddings knowledge-base)
                                            3)
          _ (println "DEBUG - √çndices similares:" similar-idxs)
          
          ;; Obter contexto relevante
          context-chunks (->> similar-idxs
                              (map #(nth (:chunks knowledge-base) %))
                              (str/join "\n\n"))
          
          ;; Se n√£o houver chunks relevantes, use o conte√∫do original
          context (if (str/blank? context-chunks)
                    (if (seq (:original-files knowledge-base))
                      (get-file-content (first (:original-files knowledge-base)))
                      "N√£o foi poss√≠vel encontrar informa√ß√µes relevantes.")
                    context-chunks)]
      
      (println "DEBUG - Tamanho do contexto:" (count context) "caracteres")
      (println "DEBUG - Amostra do contexto:" (subs context 0 (min 200 (count context))) "...")
      
      ;; Gerar resposta usando o LLM
      (llm/generate-response query context))
    "N√£o foi poss√≠vel encontrar informa√ß√µes relevantes na base de conhecimento."))

(defn -main
  "Fun√ß√£o principal que inicializa a aplica√ß√£o DocAI"
  [& _]
  (println "Inicializando DocAI...")
  
  ;; Verificar se o Ollama est√° acess√≠vel
  (println "Para usar o Ollama, certifique-se de que ele est√° em execu√ß√£o com o comando: ollama serve")
  (println "Usando o modelo deepseek-r1. Se voc√™ ainda n√£o o baixou, execute: ollama pull deepseek-r1")
  
  (let [kb (setup-knowledge-base)]
    (println "Base de conhecimento pronta! Fa√ßa sua pergunta:")
    (try
      (loop []
        (when-let [input (read-line)]
          (when-not (= input "sair")
            (println "Processando...")
            (println (query-rag kb input))
            (println "\nPr√≥xima pergunta (ou 'sair' para terminar):")
            (recur))))
      (catch Exception e
        (println "Erro: " (.getMessage e))
        (println "Detalhes: " (ex-data e))))
    (println "Obrigado por usar o DocAI. At√© a pr√≥xima!")))
```

Note que o m√≥dulo principal inclui mais logs com emojis, melhor tratamento de erros e verifica√ß√µes adicionais para garantir que o sistema n√£o falhe quando n√£o h√° chunks ou embeddings dispon√≠veis.

A estrutura de pastas do projeto fica assim:

```bash
docai/
‚îú‚îÄ‚îÄ src/
‚îÇ ‚îú‚îÄ‚îÄ docai/
‚îÇ ‚îÇ ‚îú‚îÄ‚îÄ document.clj  # Processamento de documentos
‚îÇ ‚îÇ ‚îú‚îÄ‚îÄ embedding.clj # Sistema de embeddings usando TF-IDF
‚îÇ ‚îÇ ‚îú‚îÄ‚îÄ llm.clj       # Interface com o Ollama
‚îÇ ‚îÇ ‚îî‚îÄ‚îÄ core.clj      # M√≥dulo principal
‚îú‚îÄ‚îÄ resources/
‚îÇ ‚îî‚îÄ‚îÄ docs/           # Onde ficam os documentos
‚îÇ ‚îÇ ‚îú‚îÄ‚îÄ rag.md        # Este documento sobre RAG
‚îÇ ‚îÇ ‚îî‚îÄ‚îÄ example.md    # Exemplo de documenta√ß√£o JWT
‚îú‚îÄ‚îÄ project.clj       # Configura√ß√£o do projeto
‚îî‚îÄ‚îÄ README.md
```

### Como usar?

1. Instale o Ollama seguindo as instru√ß√µes em [ollama.com](https://ollama.com)
2. Inicie o servidor Ollama:
```bash
ollama serve
```

√â necess√°rio rodar o servidor do Ollama antes de executar o projeto pois, caso contr√°rio, o projeto n√£o conseguir√° se conectar ao modelo.

3. Baixe o modelo DeepSeek R1 (ou qualquer outro modelo que preferir):

```bash
ollama pull deepseek-r1
```

4. Coloque seus documentos na pasta `resources/docs/` (j√° inclu√≠mos dois exemplos: rag.md e example.md)
5. Execute o projeto:
```bash
lein run
```

6. Fa√ßa suas perguntas! Exemplo:
```
Inicializando DocAI...
Base de conhecimento pronta! Fa√ßa sua pergunta:
Como implementar autentica√ß√£o JWT em Clojure?
Processando...
[Resposta do modelo sobre JWT baseada no example.md]
```

> **NOTA:** A prop√≥sito, o projeto docai est√° dispon√≠vel no [https://github.com/scovl/docai](https://github.com/scovl/docai) caso voc√™ queira contribuir com o projeto ou usar em outro projeto.

### Considera√ß√µes importantes 

1. **Performance**: Esta implementa√ß√£o √© b√°sica e pode ser otimizada:
   - Usando um banco de dados vetorial como [Milvus](https://milvus.io/) ou [FAISS](https://github.com/facebookresearch/faiss)
   - Implementando cache de embeddings
   - Paralelizando o processamento de chunks

2. **Mem√≥ria**: Para documenta√ß√µes muito extensas, considere:
   - Processar os chunks em lotes
   - Implementar indexa√ß√£o incremental
   - Usar streaming para arquivos grandes

3. **Modelos**: Diferentes modelos do Ollama t√™m diferentes caracter√≠sticas:
   - DeepSeek R1: Bom para compreens√£o geral e gera√ß√£o de texto
   - DeepSeek Coder: Especializado em c√≥digo
   - Llama 3: Boa alternativa geral
   - Mistral: Bom para tarefas espec√≠ficas
   - Gemma: Leve e eficiente



---

### Pr√≥ximos passos

Algumas ideias para expandir o projeto:

1.  **Tokeniza√ß√£o Avan√ßada:** Usar um tokenizador de *subpalavras* (como BPE ou WordPiece) para melhorar a busca sem√¢ntica. Idealmente, o mesmo usado no treinamento do modelo (ex: `deepseek-r1`).
2.  **Embeddings Pr√©-treinados:** Usar embeddings do pr√≥prio modelo (via Ollama) em vez de TF-IDF.  Mais simples e *muito* melhor para busca sem√¢ntica.
3.  **Banco de Dados Vetorial:** Usar um banco de dados vetorial (Milvus, FAISS, Qdrant, etc.) para lidar com *muitos* documentos de forma eficiente.
4.  **Cache:** Usar cache para os embeddings (e, opcionalmente, respostas) para acelerar o sistema.
5.  **Erros:** Tratar mais erros (Ollama offline, modelo indispon√≠vel, rede, arquivos inv√°lidos).
6.  **Logging:** Usar um framework de logging para rastreamento e depura√ß√£o.
7.  **Testes:** Adicionar testes unit√°rios e de integra√ß√£o.
8.  **Prompt Engineering:** Refinar o prompt (em `format-prompt`) para melhorar as respostas.
9.  **Usar langchain4j:** criar RAG atrav√©s do [langchain4j](https://github.com/langchain4j/langchain4j) via interop java com o clojure.
  Experimentar com:
    *   Exemplos no prompt (few-shot learning).
    *   Instru√ß√µes passo a passo (chain-of-thought).
    *   Instru√ß√µes claras sobre formato, tamanho, etc.
    *   Pedir a fonte da informa√ß√£o (qual chunk).

Bastante mais coisas podem ser feitas, mas essas s√£o as mais importantes.

---

## Langchain4j para simplificar a cria√ß√£o de RAG

At√© aqui utilizei a abordagem TF-IDF para criar embeddings e a abordagem manual para criar o RAG com intuito de apenas demonstrar o processo. O ideal, √© usar embeddings pr√©-treinados e um banco de dados vetorial para armazenar os embeddings e realizar a busca por similaridade. Para isso, vamos usar a biblioteca [Langchain4j](https://github.com/langchain4j/langchain4j).

Langchain4j √© uma biblioteca Java que oferece uma abstra√ß√£o de alto n√≠vel para construir aplica√ß√µes de IA generativa, incluindo sistemas RAG. Ela se integra bem com Clojure atrav√©s da interoperabilidade Java. Embora a implementa√ß√£o manual que fizemos anteriormente seja um √≥timo exerc√≠cio de aprendizado, usar Langchain4j pode simplificar significativamente o desenvolvimento, especialmente para aplica√ß√µes mais complexas.

Vantagens de usar Langchain4j:

- **Abstra√ß√£o:** Langchain4j fornece componentes pr√©-constru√≠dos para tarefas comuns como carregamento de documentos, divis√£o de texto, cria√ß√£o de embeddings, armazenamento vetorial e intera√ß√£o com LLMs.
- **Modularidade:** Voc√™ pode facilmente trocar diferentes implementa√ß√µes (por exemplo, usar diferentes modelos de embedding ou bancos de dados vetoriais) sem alterar o c√≥digo principal da aplica√ß√£o.
- **Integra√ß√£o:** Langchain4j oferece integra√ß√µes com v√°rias ferramentas e servi√ßos populares, incluindo Ollama, bancos de dados vetoriais (como Chroma, Weaviate, Qdrant), e modelos de linguagem de diferentes provedores.
- **Comunidade e Suporte:** Langchain4j possui uma comunidade ativa e boa documenta√ß√£o, facilitando a obten√ß√£o de ajuda e a resolu√ß√£o de problemas.

> Em um pr√≥ximo artigo, escreverei sobre como usar [Langchain4j](https://github.com/langchain4j/langchain4j) para criar um sistema RAG ainda neste mesmo projeto.


---

## Refer√™ncias

- [RAG](https://www.pinecone.io/learn/rag/) - Documenta√ß√£o do Pinecone
- [Embedding](https://www.pinecone.io/learn/embeddings/) - Documenta√ß√£o do Pinecone
- [LLM](https://www.pinecone.io/learn/llms/) - Documenta√ß√£o do Pinecone
- [Ollama](https://ollama.com/) - Ferramenta para rodar LLMs localmente
- [Clojure](https://clojure.org/) - Documenta√ß√£o do Clojure
- [http-kit](https://github.com/http-kit/http-kit) - Cliente HTTP para Clojure
- [data.json](https://github.com/clojure/data.json) - Biblioteca JSON para Clojure
- [clojure.test](https://clojure.github.io/clojure/clojure.test-api.html) - Documenta√ß√£o da biblioteca de testes do Clojure
- [clj-kondo](https://github.com/clj-kondo/clj-kondo) - Linter para Clojure